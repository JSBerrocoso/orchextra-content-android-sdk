apply plugin: 'com.android.application'

apply from: '../ocmsdk/common_methods.gradle'
apply from: '../ocmsdk/dependencies.gradle'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'applivery-plugin'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.applivery.gradle:applivery-deploy:1.0.0'
  }
}

android {
  compileSdkVersion project.ext.androidCompileSdkVersion

  signingConfigs {
    config {}
  }

  defaultConfig {
    applicationId "com.gigigo.showcase"
    minSdkVersion project.ext.androidMinSdkVersion
    targetSdkVersion project.ext.androidTargetSdkVersion
    versionCode computeVersionCode()
    versionName "1.0-RC" + computeVersionCode()

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }

  buildTypes {
    staging {
      testCoverageEnabled = false
      debuggable true
      applicationIdSuffix ".staging"
    }

    debug {
      signingConfig signingConfigs.debug
      debuggable true
      applicationIdSuffix ".debug"
    }

    release {
      signingConfig signingConfigs.debug
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt')
    }
  }

  flavorDimensions "ocm"

  productFlavors {
    ocm {
      dimension "ocm"
    }
  }

  //true to publish all flavour artifacts
  // publishNonDefault true
  defaultPublishConfig "ocmRelease"

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

configurations {
  compile.exclude group: "org.jetbrains", module: "annotations"
}

dependencies {
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.android.support:design:27.1.1'
  implementation 'com.android.support:support-v13:27.1.1'
  implementation 'com.android.support:support-v4:27.1.1'
  implementation 'com.android.support:design:27.1.1'
  implementation 'com.jakewharton.timber:timber:4.7.1'
  implementation project(path: ':ocmsdk')

  implementation 'com.android.support:multidex:1.0.3'
  implementation 'com.github.GigigoGreenLabs.imgRecogModule:vuforiaimplementation:2.6'
  implementation 'com.github.GigigoGreenLabs.imgRecogModule:vuforiaimplementation:2.6'
  implementation 'com.android.support:support-v4:27.1.1'

  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

applivery {
  try {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('applivery.properties').newDataInputStream())

    appId = "59c9197a48563b7721347736"
    apiKey = properties.getProperty("API_KEY")
  } catch (ex) {
    throw new InvalidUserDataException("You should define API_KEY in applivery.properties.")
  }
}

/**
 * Gets the commit number from the current Git commit. E.g. 538*/
def computeVersionCode() {
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-list', '--count', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim().toInteger()
}