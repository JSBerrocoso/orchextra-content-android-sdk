buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.applivery.gradle:applivery-deploy:1.0.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'applivery-plugin'

android {
  compileSdkVersion 26
  buildToolsVersion "26.0.1"

  defaultConfig {
    applicationId "com.gigigo.showcase"
    minSdkVersion 18
    targetSdkVersion 25
    versionCode computeVersionCode()
    versionName "1.0." + computeVersionCode()

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }

  buildTypes {
    debug {
      testCoverageEnabled = false
      debuggable true
      applicationIdSuffix ".debug"
    }

    quality {
      signingConfig signingConfigs.debug
      debuggable true
      applicationIdSuffix ".quality"
    }

    release {
      signingConfig signingConfigs.debug
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt')
    }
  }

  lintOptions {
    abortOnError false
  }

  configurations {
    compile.exclude group: 'org.jetbrains', module: 'annotations'
  }
}

dependencies {

  compile 'com.android.support:appcompat-v7:25.3.1'
  compile 'com.android.support:design:25.3.1'
  compile 'com.android.support:support-v13:25.3.1'

  debugCompile project(path: ':ocmsdk', configuration: 'debug')
  qualityCompile project(path: ':ocmsdk', configuration: 'quality')
  releaseCompile project(path: ':ocmsdk', configuration: 'release')

  compile 'com.android.support:multidex:1.0.1'
  compile 'com.github.GigigoGreenLabs.imgRecogModule:vuforiaimplementation:2.6'
  compile 'com.android.support:support-v4:26.1.0'
  debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
  releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

applivery {
  try {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('applivery.properties').newDataInputStream())

    appId = "59c9197a48563b7721347736"
    apiKey = properties.getProperty("API_KEY")
  } catch (ex) {
    throw new InvalidUserDataException("You should define API_KEY in applivery.properties.")
  }
}

/**
 * Gets the commit number from the current Git commit. E.g. 538*/
def computeVersionCode() {
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-list', '--count', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim().toInteger()
}
