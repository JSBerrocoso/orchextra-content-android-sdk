apply plugin: 'com.android.application'

apply from: 'variants.gradle'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

group = rootProject.ext.group
version = rootProject.ext.version

def API_KEY = "API_KEY"
def API_SECRET = "API_SECRET"
def BUSSINES_UNIT = "BUSSINES_UNIT"

android {
  signingConfigs {
    config {}
  }

  compileSdkVersion rootProject.ext.compileSdkVersion

  defaultConfig {
    minSdkVersion 18
    targetSdkVersion rootProject.ext.targetSdkVersion

    ndk {
      abiFilters "armeabi-v7a", "x86"
    }

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }

  buildTypes {
    quality {
      debuggable true
      signingConfig signingConfigs.debug
    }
    debug {
      debuggable true
      signingConfig signingConfigs.debug
    }
    prerelease {
      debuggable true
      signingConfig signingConfigs.debug
      matchingFallbacks = ['release']
    }
    release {
      minifyEnabled false
      debuggable true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig android.signingConfigs.debug
    }
  }

  productFlavors.whenObjectAdded { flavor ->
    flavor.versionCode generateAutoVersionCode()
    flavor.versionName getVersionName(appVersion)
  }

  project.android.applicationVariants.all { variant ->

    variant.productFlavors.each { flavor ->
      println("Variant: " + variant.name)
      println("Flavor:" + flavor.name)

      def flavorData = project.ext[flavor.name]
      def appName = flavorData.appName
      def appId = flavorData.applicationId

      println("**************************************************")
      println("-------------" + flavor.name + "." + variant.buildType.name + "-------------")

      println("****" + flavorData + "****")

      setVariantApplicationId(variant, appId)
      setVariantResValue(variant, appName, "app_name", STRING)

      setVariantBuildConfigField(variant, flavorData.orchextraApiKey, API_KEY, STRING)
      setVariantBuildConfigField(variant, flavorData.orchextraApiSecret, API_SECRET, STRING)
      setVariantBuildConfigField(variant, flavorData.orchextraBussinesUnit, BUSSINES_UNIT, STRING)

      println("--**********************************************************--")
    }
  }

  flavorDimensions "sample"

  productFlavors {
    showRoom {
      dimension "sample"
    }

    woah_Showcase {
      dimension "sample"
    }

    woah_QA {
      dimension "sample"
    }

    woah_DEMO {
      dimension "sample"
    }

    woah_SecurityScan {
      dimension "sample"
    }

    woah_CSE_2017 {
      dimension "sample"
    }

    woah_IT {
      dimension "sample"
    }

    ferring {
      dimension "sample"
    }

    woah_HU {
      dimension "sample"
    }

    woah_PL {
      dimension "sample"
    }

    woah_RS {
      dimension "sample"
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8 //this is 4 rx, no problem with ox 1_7
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

configurations {
  cleanedAnnotations
  compile.exclude group: 'org.jetbrains', module: 'annotations'
}

dependencies {
  implementation project(path: ':ocmsdk')
  implementation 'androidx.multidex:multidex:2.0.1'

  /*New dependencies*/
  qualityImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
  debugImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
  prereleaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

  implementation 'com.facebook.stetho:stetho:1.5.1'

  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  implementation 'com.google.android.material:material:1.0.0'
  implementation project.libraries.timber
}
