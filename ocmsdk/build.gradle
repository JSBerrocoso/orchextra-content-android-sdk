apply plugin: 'com.android.library'
apply from: 'jacoco.gradle'
apply from: 'sonarqube.gradle'
apply from: 'common_methods.gradle'
apply from: 'dependencies.gradle'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.dcendents.android-maven'
apply from: 'variants.gradle'

group = 'com.github.Orchextra'

def CONCURRENT_INTERACTORS = "CONCURRENT_INTERACTORS"
def API_URL = "API_URL"
def NETWORK_LOG = "NETWORK_LOG"
def OCM_SDK_VERSION = "OCM_SDK_VERSION"
def YOUTUBE_DEVELOPER_KEY = "YOUTUBE_DEVELOPER_KEY"
def VIMEO_ACCESS_TOKEN = "VIMEO_ACCESS_TOKEN"

android {
  compileSdkVersion project.androidCompileSdkVersion
  buildToolsVersion project.androidBuildToolsVersion

  defaultConfig {
    minSdkVersion project.androidMinSdkVersion
    targetSdkVersion project.androidTargetSdkVersion
    versionCode 1
    versionName "1.0"
    ndk {
      abiFilters "armeabi-v7a"
    }
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    buildConfigField STRING, VIMEO_ACCESS_TOKEN, project.vimeoAccessToken
    buildConfigField INT, CONCURRENT_INTERACTORS, project.concurrentInteractors
  }

  buildTypes {
    debug {
      debuggable true

      buildConfigField STRING, API_URL, '"https://cm.s.orchextra.io/"'
      buildConfigField STRING, OCM_SDK_VERSION, '"ANDROID_1.0.0"'
      buildConfigField STRING, YOUTUBE_DEVELOPER_KEY, project.youtubeDevelopKey
      buildConfigField BOOLEAN, NETWORK_LOG, "true"
    }

    quality {
      debuggable true

      buildConfigField STRING, API_URL, '"https://cm.q.orchextra.io/"'
      buildConfigField STRING, OCM_SDK_VERSION, '"ANDROID_1.0.0"'
      buildConfigField STRING, YOUTUBE_DEVELOPER_KEY, project.youtubeDevelopKey
      buildConfigField BOOLEAN, NETWORK_LOG, "true"
    }

    release {
      debuggable false
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField STRING, API_URL, '"https://cm.orchextra.io/"'
      buildConfigField STRING, OCM_SDK_VERSION, '"ANDROID_1.0.0"'
      buildConfigField STRING, YOUTUBE_DEVELOPER_KEY, project.youtubeReleaseKey
      buildConfigField BOOLEAN, NETWORK_LOG, "false"
    }
  }

  lintOptions {
    quiet true
    abortOnError false
    ignoreWarnings true
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8 //this is 4 rx, no problem with ox 1_7
    targetCompatibility JavaVersion.VERSION_1_8
  }

  publishNonDefault true

  testOptions {
    unitTests.returnDefaultValues = true
  }
}

jacocoAndroidUnitTestReport {
  excludes += ['**/BuildConfig*.*']
}

dependencies {
  //TEST
  testImplementation project.testLibraries.junit
  testImplementation project.testLibraries.mockito

  implementation fileTree(dir: 'libs', include: ['*.jar']) //youtube

  //google Support/CustomTabs/playServices
  implementation project.libraries.androidSupportDesign
  implementation project.libraries.androidCustomtabs

  implementation project.libraries.playServicesBase
  implementation project.libraries.playServicesLocation
  implementation project.libraries.playServicesGcm

  //gigigo Lib
  implementation project.libraries.ggglib
  implementation project.libraries.gggJavaLib

  //Dagger
  implementation project.libraries.dagger
  annotationProcessor project.libraries.daggerCompiler

  //Image Loader Glide/Blur
  api project.libraries.imageloaderGlideBlur

  implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"
  implementation 'com.github.GigigoGreenLabs.imgRecogModule:imagerecognitioninterface:2.6'

  //Orchextra
  // implementation project.libraries.oxvuforiaimplementation
  debugApi(project.libraries.orchextrasdkStaging) {
    transitive true
    exclude module: 'support-v4'
  }
  qualityApi(project.libraries.orchextrasdkDebug) {
    transitive true
    exclude module: 'support-v4'
  }
  releaseApi(project.libraries.orchextrasdkRelease) {
    transitive true
    exclude module: 'support-v4'
  }

  //customs Views
  implementation project.libraries.doubleViewPager
  implementation project.libraries.autofitTextView
  implementation project.libraries.multipleGridRecyclerView

  implementation project.libraries.rxAndroid
  implementation project.libraries.rxJava
  implementation project.libraries.rxRetrofit
  implementation project.libraries.arrow

  implementation project.libraries.kache

  implementation 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1'

  api project(':vimeosdk')

  //TEST
  testImplementation project.testLibraries.junit
  testImplementation project.testLibraries.assertj
  testImplementation project.testLibraries.mockito
}