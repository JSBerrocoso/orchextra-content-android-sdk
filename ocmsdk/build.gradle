apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.dcendents.android-maven'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: 'jacoco.gradle'
apply from: 'variants.gradle'

group = rootProject.ext.group
version = rootProject.ext.version

ext {
    bintrayRepo = 'maven'
    bintrayName = 'com.gigigo.ocm:content'

    publishedGroupId = 'com.gigigo.ocm'
    libraryName = 'Orchextra Content Manager'
    artifact = 'ocmsdk'

    libraryDescription = 'Orchextra Content Manager'

    siteUrl = 'https://github.com/Orchextra/orchextra-content-android-sdk'
    gitUrl = 'https://github.com/Orchextra/orchextra-content-android-sdk.git'

    libraryVersion = rootProject.ext.version

    developerId = 'gigigo-desarrollo'
    developerName = 'Android Gigigo'
    developerEmail = 'gigigo.desarrollo@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

def OCM_DIMENSION = "ocm"
def CONCURRENT_INTERACTORS = "CONCURRENT_INTERACTORS"
def RETROFIT_LOG = "RETROFIT_LOG"
def API_URL = "API_URL"
def OCM_SDK_VERSION = "OCM_SDK_VERSION"
def YOUTUBE_DEVELOPER_KEY = "YOUTUBE_DEVELOPER_KEY"
def VIMEO_ACCESS_TOKEN = "VIMEO_ACCESS_TOKEN"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode generateAutoVersionCode()
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    packagingOptions {
        exclude 'lib/mips/*'
        exclude 'lib/mips64/*'
        exclude 'lib/x86/*'
        exclude 'lib/x86_64/*'
        exclude 'lib/armeabi/*'
        exclude 'lib/arm64-v8a/*'
    }

    buildTypes {
        debug {
            debuggable true

            version = "${rootProject.ext.version}-D"
            project.ext.libraryVersion = "${rootProject.ext.version}-D"
        }
        quality {
            debuggable true

            version = "${rootProject.ext.version}-Q"
            project.ext.libraryVersion = "${rootProject.ext.version}-Q"
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig null
        }
    }

    project.android.libraryVariants.all { variant ->
        variant.productFlavors.each { flavor ->
            setVariantBuildConfigField(variant, apiOcm, API_URL, STRING)
            setVariantBuildConfigField(variant, version, OCM_SDK_VERSION, STRING)
            setVariantBuildConfigField(variant, concurrentInteractors, CONCURRENT_INTERACTORS, INT)
            setVariantBuildConfigField(variant, retrofitLog, RETROFIT_LOG, BOOLEAN)
            setVariantBuildConfigField(variant, youtubeDeveloperKey, YOUTUBE_DEVELOPER_KEY, STRING)
            setVariantBuildConfigField(variant, vimeoAccessToken, VIMEO_ACCESS_TOKEN, STRING)
            println("flavor: " + flavor.name)
            println("--**********************************************************--")
        }
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8 //this is 4 rx, no problem with ox 1_7
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }


    flavorDimensions OCM_DIMENSION
    productFlavors {
        ocm {
            dimension OCM_DIMENSION
        }
    }
}

jacocoAndroidUnitTestReport {
    excludes += ['**/BuildConfig*.*']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar']) //youtube

    //google Support/CustomTabs/playServices
    implementation project.libraries.androidSupportDesign
    implementation project.libraries.androidCustomtabs

    implementation project.libraries.playServicesBase
    implementation project.libraries.playServicesLocation
    implementation project.libraries.playServicesGcm

    //gigigo Lib
    implementation project.libraries.ggglib
    implementation project.libraries.gggJavaLib

    //Dagger
    kapt project.libraries.daggerCompiler
    kapt project.libraries.daggerProcessor
    implementation project.libraries.dagger

    //Image Loader Glide/Blur
    implementation project.libraries.glide
    implementation project.libraries.glideTransformations

    // ORCHEXTRA
    qualityImplementation project.libraries.oxCoreQuality
    debugImplementation project.libraries.oxCoreDebug
    releaseImplementation project.libraries.oxCoreRelease

    qualityImplementation project.libraries.oxGeofenceQuality
    debugImplementation project.libraries.oxGeofenceDebug
    releaseImplementation project.libraries.oxGeofenceRelease

    qualityImplementation project.libraries.oxIndoorpositioningQuality
    debugImplementation project.libraries.oxIndoorpositioningDebug
    releaseImplementation project.libraries.oxIndoorpositioningRelease

    qualityImplementation project.libraries.oxScannerQuality
    debugImplementation project.libraries.oxScannerDebug
    releaseImplementation project.libraries.oxScannerRelease

    // Customs Views
    implementation project.libraries.doubleViewPager
    implementation project.libraries.autofitTextView
    implementation project.libraries.multipleGridRecyclerView
    implementation project.libraries.baserecycleradapter

    implementation project.libraries.rxAndroid
    implementation project.libraries.rxJava
    implementation project.libraries.rxRetrofit
    implementation project.libraries.arrow

    implementation project.libraries.kache
    implementation project.libraries.timber

    implementation project.libraries.kotlinStdLib
    implementation project.libraries.coroutinesCore
    implementation project.libraries.coroutinesAndroid

    kapt project.libraries.archLifecycleCompiler
    kapt project.libraries.archPersistenceCompiler
    implementation project.libraries.archLifecycleExtensions
    implementation project.libraries.archPersistenceRuntime
    implementation project.libraries.archPersistenceRxJava2

    implementation project.libraries.gpuImage //4 read articles
    implementation project.libraries.vimeoNetworking
    implementation project.libraries.retrofit
    implementation project.libraries.okhttp3
    implementation project.libraries.loggingInterceptor
    implementation project.libraries.exoplayer
    implementation project.libraries.zbar

    // Modules
    implementation project.modules.vimeo

    //TEST
    testImplementation project.libraries.archPersistenceTesting
    testImplementation project.testLibraries.junit
    testImplementation project.testLibraries.assertj
    testImplementation project.testLibraries.mockito
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'

